/*
 * Copyright (c) 2015. Ronald D. Kurr kurr@jvmguy.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        maven {
            url = artifactory
        }
    }
    dependencies {
        classpath "io.ratpack:ratpack-gradle:${ratpackVersion}"
        classpath "com.github.jengelman.gradle.plugins:shadow:${shadowVersion}"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.1"
}

description = 'Quick starting JVM web application that is helpful in showcasing load balanced microservices.'
group = 'org.kurron.example'
ext.builtOn = new Date()

// Show how to exclude objects from being required to have a unit-level test
ext.coverageExcludes = [
    'org/kurron/example/'
]

apply plugin: 'build-announcements'

apply from: "$rootDir/gradle/repositories.gradle"
apply from: "$rootDir/gradle/version.gradle"
apply from: "$rootDir/gradle/groovy.gradle"
apply from: "$rootDir/gradle/maven-publish.gradle"
apply from: "$rootDir/gradle/spock.gradle"
apply from: "$rootDir/gradle/dependency-exclusions.gradle"
apply from: "$rootDir/gradle/conflicts.gradle"
apply from: "$rootDir/gradle/cucumber.gradle"
apply from: "$rootDir/gradle/bintray.gradle"
apply from: "$rootDir/gradle/codenarc.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"

apply plugin: 'io.ratpack.ratpack-groovy'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'org.kurron.example.MainGroovy'

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}:indy"
    compile "org.kurron:tools-groovy:${toolsGroovyVersion}"
    compile "io.ratpack:ratpack-groovy:${ratpackVersion}"

    runtime "net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}"
}

defaultTasks << 'clean'
defaultTasks << 'shadowJar'

// only publish artifacts if publishArtifacts is true, and the branch is either master or development
if ( 'true' == publishArtifacts && branch in ['master', 'development'] ) {
    // only use this if you decide not to use the Bintray plugin
    // defaultTasks << 'publish'
    defaultTasks << 'bintrayUpload'
}
else {
    defaultTasks << 'publishToMavenLocal'
}

